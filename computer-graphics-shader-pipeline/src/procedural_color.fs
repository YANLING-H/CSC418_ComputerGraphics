// Set the pixel color to an interesting procedural color generated by mixing
// and filtering Perlin noise of different frequencies.
//
// Uniforms:
uniform mat4 view;
uniform mat4 proj;
uniform float animation_seconds;
uniform bool is_moon;
// Inputs:
in vec3 sphere_fs_in;
in vec3 normal_fs_in;
in vec4 pos_fs_in; 
in vec4 view_pos_fs_in; 
// Outputs:
out vec3 color;

// expects: blinn_phong, perlin_noise
void main()
{
  // Copy from lit.fs
  float light_dist = 6.0;
  float light_orbit_period = 8.0;
  float theta = -mod(animation_seconds, light_orbit_period) * 2 * M_PI / light_orbit_period;

  vec4 light_pos = vec4(light_dist, light_dist, light_dist, 1.0);
  mat4 light_transform = mat4(
  cos(theta), 0, -sin(theta), 0,
           0, 1,           0, 0,
  sin(theta), 0,  cos(theta), 0,
           0, 0,           0, 1);
  vec4 light = view * light_transform * light_pos;

  vec3 ka, ks, kd;
  float p;
  vec3 n, v, l;

  // Generate Noise

  // Two random vectors to influence seed in perlin noise
  vec3 v1 = vec3(2.0, 15.0, 10.0);
  vec3 v2 = vec3(22.0, 18.0, 1.5);

  float object_factor;  // Some value to differentiate earth and moon so they dont have the exact patterns
  if (is_moon)
    object_factor = 1.4;
  else
    object_factor = 0.9;

  float noise1 = perlin_noise(v1 * object_factor * sphere_fs_in) + 0.1 * perlin_noise(v2 * object_factor *  sphere_fs_in);
  float noise2 = perlin_noise(v1 * object_factor *  sphere_fs_in) + 0.2 * perlin_noise(v2 * object_factor *  sphere_fs_in);
  float noise3 = perlin_noise(v1 * object_factor *  sphere_fs_in) + 0.3 * perlin_noise(v2 * object_factor *  sphere_fs_in);

  float noise = object_factor * perlin_noise(object_factor * vec3(noise1, noise2, noise3));
  float c_noise = perlin_noise((1.0 + object_factor) * sphere_fs_in);

  if (is_moon) {
    ka = vec3(0.12, 0.1, 0.1);
    ks = vec3(0.9, 0.9, 0.9);
    kd = vec3(0.475, 0.4, 0.4);
    p = 1000;
  } else{
    ka = vec3(0.2, 0.3, 0.5);
    ks = vec3(0.6, 0.7, 0.7);
    kd = vec3(0.1, 0.3, 0.8);
    p = 500;
  }

  // Apply noise to values
  float noise_factor = 1.0 + object_factor * noise;
  if (noise_factor != 0) {
    kd *= noise_factor;
    ka *= noise_factor;
    ks *= noise_factor;
  }

  n = normalize(normal_fs_in);
  v = -normalize(view_pos_fs_in.xyz);
  l = normalize(light.xyz - view_pos_fs_in.xyz);

  color = blinn_phong(ka, kd, ks, p, n, v, l);
  // Apply noise to color output
  color *= vec3(1.0 - c_noise);
}

